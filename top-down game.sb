'=================== MAIN SUBS  ==============
'=================== MAIN SUBS  ==============
'=================== MAIN SUBS  ==============
Sub setup
  notEnd = "true"
  width = 800
  height = 680
  fps = 50
  moneyPerDiamond = 10
  
  spiders = ""
  indexSpider = 1
  
  score = 0
  money = 0
  
  ' game process variables
  globalLvl = 1
  vave2IsShowed = "false"
  vave3IsShowed = "false"
  victory = "false"
  
  ' Массив пауз
  pauses[1] = 4
  pauses[2] = 5
  pauses[3] = 6
  pauses[4] = 7
  randomPause = pauses[Math.GetRandomNumber(4)]
  
  directory = Program.Directory
  
  GraphicsWindow.Width = width
  GraphicsWindow.Height = height
  GraphicsWindow.CanResize = "false"
  GraphicsWindow.BackgroundColor = "#472D3C"
  
  initWorld()
  initMenu()
  createPlayer()
  createEnemySpider()
  updateScoreText()
  updateMoneyText()
  updateSpeedBulletsText()
  updateHpMenu()
  
  message = "1 wave!"
  showMessage()
EndSub

Sub loop
  checkPlayerEdjes()
  checkCollisionPlayerDiamond()
  checkCollisionPlayerFire()
  moveBullet()
  bulletLife()
  moveSpiders()
  checkEdjesSpiders()
  checkCollisionBulletSpider()
  checkCollisionPlayerSpider()
  checkCollisionSpiderDiamond()
  
  frames = frames+1
  If Math.Remainder(frames,randomPause*fps) = 0 Then
    createEnemySpider()
    randomPause = pauses[Math.GetRandomNumber(4)]
  EndIf
  If Math.Remainder(frames,randomPause*2*fps) = 0 Then
    x = leftBorder + Math.GetRandomNumber(width-80-rightBorder)
    y = topBorder + Math.GetRandomNumber(height-80-bottomBorder)
    createDiamond()
    indexDiamond = indexDiamond + 1
  EndIf
  If Math.Remainder(frames,randomPause*3*fps) = 0 Then
    randomDiamondIndex = Math.GetRandomNumber(Array.GetItemCount(diamonds))
    diamonds[randomDiamondIndex]["collected"] = "false"
    Shapes.ShowShape(diamonds[randomDiamondIndex]["diamond"])
  EndIf
  
  If playerHp <= 0 Then
    notEnd = "false"
    Shapes.HideShape(player)
    player = Shapes.AddImage(deadPlayerImage)
    Shapes.Move(player,playerX,playerY)
    message = "You dead!"
    showMessage()
  EndIf
  
  If indexSpider >= 10 And indexSpider < 20 Then
    globalLvl = 2
    If vave2IsShowed = "false" Then
      vave2IsShowed = "true"
      message = "2 wave!"
      showMessage()
    EndIf
  ElseIf indexSpider >=20 And indexSpider < 30 Then
    globalLvl = 3
    If vave3IsShowed = "false" Then
      vave3IsShowed = "true"
      message = "3 wave!"
      showMessage()
    EndIf
  ElseIf spidersKilled >=30 Then
    victory = "true"
    message = "Victory!"
    showMessage()
    notEnd = "false"
  EndIf
EndSub

'=================== MESSAGES  ==============
'=================== MESSAGES  ==============
Sub showMessage
  GraphicsWindow.BrushColor = "#A4EF06"
  GraphicsWindow.FontSize = "30"
  messageText = Shapes.AddText(message)
  Shapes.Move(messageText, width/2 - 60, (height-bottomBorder)/2)
  GraphicsWindow.KeyDown = emptySub
  Program.Delay(3000)
  GraphicsWindow.KeyDown = keyboardEvent
  GraphicsWindow.BrushColor = "White"
  Shapes.HideShape(messageText)
EndSub

'=================== EVENT LISTENERS  ==============
'=================== EVENT LISTENERS  ==============
'=================== EVENT LISTENERS  ==============
GraphicsWindow.KeyDown = keyboardEvent

Sub keyboardEvent
  lastKey = GraphicsWindow.LastKey
  If lastKey = "W" Then
    playerY = playerY - playerSpdY
    movePlayer()
  ElseIf lastKey = "S" Then
    playerY = playerY + playerSpdY
    movePlayer()
  ElseIf lastKey = "A" Then
    playerX = playerX - playerSpdX
    movePlayer()
  ElseIf lastKey = "D" Then
    playerX = playerX + playerSpdX
    movePlayer()
  EndIf
EndSub

Sub emptySub
EndSub

GraphicsWindow.MouseDown = mouseEvent

Sub mouseEvent
  mouseX = GraphicsWindow.MouseX
  mouseY = GraphicsWindow.MouseY
  performAttack()
EndSub

'=================== SKILLS MENU ==============
Sub initMenu
  GraphicsWindow.BrushColor = "Black"
  GraphicsWindow.PenColor = "Green"
  GraphicsWindow.FontSize = 16
  buyBulletBtn = Controls.AddButton("Buy bullet (-3)",20,620)
  Controls.SetSize(buyBulletBtn,240,40)
  increaseBulletSpeedBtn = Controls.AddButton("Bullet speed (-20)",280,620)
  Controls.SetSize(increaseBulletSpeedBtn,240,40)
EndSub

Controls.ButtonClicked = whichBtn

Sub whichBtn
  clickedBtn = Controls.LastClickedButton
  If clickedBtn = buyBulletBtn Then
    ' buy bullet
    If money >= 3 Then
      money = money - 3
      updateMoneyText()
      bulletNumber = bulletNumber + 1
      updateBulletsText()
    Else 
    EndIf
  ElseIf clickedBtn = increaseBulletSpeedBtn Then
    ' increase bullet speed
    If money >= 20 Then
      money = money - 20
      updateMoneyText()
      score = score+2
      updateScoreText()
      bulletSpeed = bulletSpeed+1
      updateSpeedBulletsText()
    Else 
    EndIf
  EndIf
EndSub

'===================  STATES (BULLETS, SCORE, MONEY, HP)  ==============
Sub updateScoreText
  Shapes.HideShape(scoreText)
  GraphicsWindow.FontSize = 20
  GraphicsWindow.BrushColor = "White"
  scoreText = Shapes.AddText("Score: " + score)
  Shapes.Move(scoreText,10,5)
  Shapes.ShowShape(scoreText)
EndSub

Sub updateMoneyText
  Shapes.HideShape(moneyText)
  GraphicsWindow.FontSize = 20
  GraphicsWindow.BrushColor = "White"
  moneyText = Shapes.AddText("$: " + money)
  Shapes.Move(moneyText,120,5)
  Shapes.ShowShape(moneyText)
EndSub

Sub updateBulletsText
  Shapes.HideShape(bulletText)
  GraphicsWindow.FontSize = 20
  GraphicsWindow.BrushColor = "White"
  bulletText = Shapes.AddText("Bullets: " + bulletNumber)
  Shapes.Move(bulletText,230,5)
  Shapes.ShowShape(bulletText)
EndSub

Sub updateSpeedBulletsText
  Shapes.HideShape(bulletSpeedText)
  GraphicsWindow.FontSize = 20
  GraphicsWindow.BrushColor = "White"
  bulletSpeedText = Shapes.AddText("Bullet Speed: " + bulletSpeed)
  Shapes.Move(bulletSpeedText,440,5)
  Shapes.ShowShape(bulletSpeedText)
EndSub

Sub updateHpMenu
  xPositionForHpIndicators = width - 40 - playerMaxHealth*40 - (playerMaxHealth-1)*10
  hpFullImage = directory + "/objects/player-hp-full.png"
  hpEmptyImage = directory + "/objects/player-hp-empty.png"
  For i=1 To playerMaxHealth 
    Shapes.HideShape(hearts[i])
    If playerHp >= i Then
      hearts[i] = Shapes.AddImage(hpFullImage)
      Shapes.Move(hearts[i], xPositionForHpIndicators, height-60)
    Else 
      hearts[i] = Shapes.AddImage(hpEmptyImage)
      Shapes.Move(hearts[i], xPositionForHpIndicators, height-60)
    EndIf
    xPositionForHpIndicators = xPositionForHpIndicators + 50
  EndFor
EndSub

'=================== GAME WORLD  ==============
'=================== GAME WORLD  ==============
'=================== GAME WORLD  ==============
Sub initWorld
  topBorder = 40
  leftBorder = 40
  rightBorder = 40
  bottomBorder = 120
  worldImg = directory + "/world.png"
  world = Shapes.AddImage(worldImg)
  fillWorld()
EndSub

Sub fillWorld
  indexFire = 1
  indexDiamond = 1
  fires = ""
  diamonds = ""
  x = 40
  y = 40
  For i=1 To 13
    ' Create random index for diamond in every world row
    randomIndexForDiamond = Math.GetRandomNumber(18)
    For j=1 To 18
      If j = randomIndexForDiamond Then
          ' diamond
          fillWorldImg = directory + "/objects/w10.png"
          createDiamond()
          indexDiamond = indexDiamond + 1
      EndIf
      If i=13 And j=18 Then
      ElseIf i=1 And j=1 Then
      ElseIf j = randomIndexForDiamond Then
      Else
        probability = Math.GetRandomNumber(100)
        If probability <= 5 Then
          ' fire
          fillWorldImg = directory + "/objects/w11.png"
          createFire()
          indexFire = indexFire + 1
        Else
          ' ordinary ground
          randomNum = Math.GetRandomNumber(9)
          fillWorldImg = directory + "/objects/w" + randomNum + ".png"
          fillWorldPart = Shapes.AddImage(fillWorldImg)
          Shapes.Move(fillWorldPart,x,y)
        EndIf
      EndIf
      x = x + 40
    EndFor
    x = 40
    y = y + 40
  EndFor
EndSub
  
'=================== PLAYER  ==============
'=================== PLAYER  ==============
'=================== PLAYER  ==============
Sub createPlayer
  playerMaxHealth = 3
  deadPlayerImage = directory + "/objects/player-dead.png"
  playerImg = directory + "/objects/player.png"
  playerW = 40
  playerH = 40
  playerX = 40
  playerY = 40 
  playerHp = 3
  playerAimAngle = 0
  playerSpdY = 20
  playerSpdX = 20
  bulletNumber = 3
  player = Shapes.AddImage(playerImg)
  Shapes.Move(player,playerX,playerY)
  
  createBullet()
  updateBulletsText()
EndSub

Sub movePlayer
  Shapes.Move(player,playerX,playerY)
EndSub

Sub checkPlayerEdjes
  If playerX > width-playerW-rightBorder Then
    playerX = playerX - playerSpdX
    movePlayer()
  ElseIf playerX < leftBorder Then
    playerX = playerX + playerSpdX
    movePlayer()
  EndIf
  If playerY > height-playerH-bottomBorder Then
    playerY = playerY - playerSpdY
    movePlayer()
  ElseIf playerY < topBorder Then
    playerY = playerY + playerSpdY
    movePlayer()
  EndIf
EndSub

'=================== BULLET  ==============
'=================== BULLET  ==============
'=================== BULLET  ==============
Sub createBullet
  bulletX = playerX + playerW/2 - 5
  bulletY = playerY + playerH/2 - 5
  bulletW = 10
  bulletH = 10
  bulletTimeOfLife = 2
  bulletSpeedX = 0
  bulletSpeedY = 0
  bulletSpeed = 3
  bulletImg = directory + "/objects/bullet.png"
  bullet = Shapes.AddImage(bulletImg)
  Shapes.HideShape(bullet)
  Shapes.Move(bullet,bulletX,bulletY)
EndSub

Sub performAttack
  If bulletNumber >= 1 Then
    bulletNumber = bulletNumber - 1
    bulletIsFlying = "true"
    bulletX = playerX + playerW/2 - 5
    bulletY = playerY + playerH/2 - 5
    bulletDuration = 0
    Shapes.Move(bullet,bulletX,bulletY)
    koeff = 90*100/150
    diffX = mouseX - playerX
    diffY = mouseY - playerY
    If diffX > 0 Then
      playerAimAngle = Math.ArcTan(diffY/diffX)*koeff
    ElseIf diffX < 0 And diffY >= 0 Then
      playerAimAngle = Math.ArcTan(diffY/diffX)*koeff + 180
    ElseIf diffX < 0 And diffY < 0 Then
      playerAimAngle = Math.ArcTan(diffY/diffX)*koeff - 180
    EndIf
    angle = playerAimAngle
    
    bulletSpeedX = Math.cos(angle/180*Math.Pi)*bulletSpeed
    bulletSpeedY = Math.Sin(angle/180*Math.Pi)*bulletSpeed
    Shapes.ShowShape(bullet)
    updateBulletsText()
   EndIf
EndSub

Sub moveBullet
  bulletX = bulletX + bulletSpeedX
  bulletY = bulletY + bulletSpeedY
  Shapes.Move(bullet,bulletX,bulletY)
EndSub

Sub bulletLife
  If bulletIsFlying = "true" Then
    bulletDuration = bulletDuration + 1
    If bulletX > width-bulletW-rightBorder Or bulletX < leftBorder Or bulletY > height-bulletH-bottomBorder Or bulletY < topBorder Then
      hideBullet()
    EndIf
    If bulletDuration > bulletTimeOfLife*1000/fps Then
      hideBullet()
    EndIf
  EndIf
EndSub

Sub hideBullet
  Shapes.HideShape(bullet)
  bulletSpeedX = 0
  bulletSpeedY = 0
  bulletIsFlying = "false"
  bulletX = 0
  bulletY = 0
  Shapes.Move(bullet,bulletX,bulletY)
EndSub

'=================== GAME OBJECTS ==============
'=================== GAME OBJECTS ==============
'=================== GAME OBJECTS ==============

'=================== SPIDER  ==============
Sub createEnemySpider
  spiderImg = directory + "/objects/enemy-spider-" + globalLvl + ".png"
  spiders[indexSpider]["width"] = 40
  spiders[indexSpider]["height"] = 40
  spiders[indexSpider]["x"] = leftBorder + Math.GetRandomNumber(width - rightBorder-80)
  spiders[indexSpider]["y"] = topBorder + Math.GetRandomNumber(height - bottomBorder-80)
  ' New spider can't detect with player
  While Math.Abs(spiders[indexSpider]["x"]-playerX) < 100 And Math.Abs(spiders[indexSpider]["y"]-playerY) < 100
    spiders[indexSpider]["x"] = leftBorder + Math.GetRandomNumber(width - rightBorder-80)
    spiders[indexSpider]["y"] = topBorder + Math.GetRandomNumber(height - bottomBorder-80)
  EndWhile
  ' Hp
  spiders[indexSpider]["lvl"] = globalLvl
  spiders[indexSpider]["hp"] = globalLvl
  spiders[indexSpider]["speedX"] = globalLvl+1 - Math.GetRandomNumber((globalLvl+1)*2) 
  spiders[indexSpider]["speedY"] = globalLvl+1 - Math.GetRandomNumber((globalLvl+1)*2) 
  While spiders[indexSpider]["speedX"] = 0 Or spiders[indexSpider]["speedY"] = 0
    spiders[indexSpider]["speedX"] = 2 - Math.GetRandomNumber(4) 
    spiders[indexSpider]["speedY"] = 2 - Math.GetRandomNumber(4) 
  EndWhile
  spiders[indexSpider]["spider"] = Shapes.AddImage(spiderImg)
  Shapes.Move(spiders[indexSpider]["spider"],spiders[indexSpider]["x"],spiders[indexSpider]["y"])
  indexSpider = indexSpider + 1
EndSub

Sub moveSpiders
  For i=1 To Array.GetItemCount(spiders)
    spiders[i]["x"] = spiders[i]["x"] + spiders[i]["speedX"]
    spiders[i]["y"] = spiders[i]["y"] + spiders[i]["speedY"]
    Shapes.Move(spiders[i]["spider"],spiders[i]["x"],spiders[i]["y"])
  EndFor
EndSub

Sub checkEdjesSpiders
  For i=1 To Array.GetItemCount(spiders)
    If spiders[i]["x"] > width-spiders[i]["width"]-rightBorder Or spiders[i]["x"] < leftBorder Then
      spiders[i]["speedX"] = spiders[i]["speedX"]*-1
    EndIf
    If spiders[i]["y"] > height-spiders[i]["height"]-bottomBorder Or spiders[i]["y"] < topBorder Then
      spiders[i]["speedY"] = spiders[i]["speedY"]*-1
    EndIf
  EndFor
EndSub

Sub checkCollisionBulletSpider
  For i=1 To Array.GetItemCount(spiders)
    If bulletX < spiders[i]["x"]+spiders[i]["width"] And bulletX + bulletW > spiders[i]["x"] And bulletY < spiders[i]["y"]+ spiders[i]["height"] And bulletY + bulletH > spiders[i]["y"] Then 
      If spiders[i]["lvl"] = 1 Then
        ' hide bullet
        hideBullet()
        score = score+1
        updateScoreText()
        money = money+spiders[i]["lvl"]*2
        updateMoneyText()
        ' hide spider
        Shapes.HideShape(spiders[i]["spider"])
        spiders[i] = ""
        spidersKilled = spidersKilled + 1
      ElseIf spiders[i]["lvl"] = 2 Then
        If spiders[i]["hp"] = 2 Then
          ' Spider is damaged
          hideBullet()
          spiders[i]["hp"] = spiders[i]["hp"] - 1
          spiders[i]["speedX"] = spiders[i]["speedX"]*0.6
          spiders[i]["speedY"] = spiders[i]["speedY"]*0.6
          newDamagedImage = directory + "/objects/enemy-spider-damaged-" + spiders[i]["hp"] + "-" + spiders[i]["lvl"] + ".png"
          Shapes.HideShape(spiders[i]["spider"])
          spiders[i]["spider"] = Shapes.AddImage(newDamagedImage)
        Else
          ' Spider is killed
          ' hide bullet
          hideBullet()
          score = score+2
          updateScoreText()
          money = money+spiders[i]["lvl"]*2
          updateMoneyText()
          Shapes.HideShape(spiders[i]["spider"])
          spiders[i] = ""
          spidersKilled = spidersKilled + 1
        EndIf
      Else 
        If spiders[i]["hp"] = 3 Then
          ' Spider is damaged
          hideBullet()
          spiders[i]["hp"] = spiders[i]["hp"] - 1
          spiders[i]["speedX"] = spiders[i]["speedX"]*0.7
          spiders[i]["speedY"] = spiders[i]["speedY"]*0.7
          newDamagedImage = directory + "/objects/enemy-spider-damaged-" + spiders[i]["hp"] + "-" + spiders[i]["lvl"] + ".png"
          Shapes.HideShape(spiders[i]["spider"])
          spiders[i]["spider"] = Shapes.AddImage(newDamagedImage)
        ElseIf spiders[i]["hp"] = 2 Then
          ' Spider is almost dead
          hideBullet()
          spiders[i]["hp"] = spiders[i]["hp"] - 1
          spiders[i]["speedX"] = spiders[i]["speedX"]*0.7
          spiders[i]["speedY"] = spiders[i]["speedY"]*0.7
          newDamagedImage = directory + "/objects/enemy-spider-damaged-" + spiders[i]["hp"] + "-" + spiders[i]["lvl"] + ".png"
          Shapes.HideShape(spiders[i]["spider"])
          spiders[i]["spider"] = Shapes.AddImage(newDamagedImage)
        Else
          ' Spider is killed
          ' hide bullet
          hideBullet()
          score = score+3
          updateScoreText()
          money = money+spiders[i]["lvl"]*2
          updateMoneyText()
          Shapes.HideShape(spiders[i]["spider"])
          spiders[i] = ""
          spidersKilled = spidersKilled + 1
        EndIf
      EndIf
    EndIf
  EndFor
EndSub


Sub checkCollisionPlayerSpider
  For i=1 To Array.GetItemCount(spiders)

    If playerX < spiders[i]["x"]+spiders[i]["width"] And playerX + playerW > spiders[i]["x"] And playerY < spiders[i]["y"]+ spiders[i]["height"] And playerY + playerH > spiders[i]["y"] Then 
      ' hide spider
      Shapes.HideShape(spiders[i]["spider"])
      spiders[i] = ""
      '  damages to player
      playerHp = playerHp - 1
      updateHpMenu()
    EndIf
  EndFor
EndSub

Sub checkCollisionSpiderDiamond
  For i=1 To Array.GetItemCount(spiders)
    For j=1 To Array.GetItemCount(diamonds)
      If spiders[i]["x"] < diamonds[j]["x"]+diamonds[j]["width"] And spiders[i]["x"] + spiders[i]["width"] > diamonds[j]["x"] And spiders[i]["y"] < diamonds[j]["y"]+ diamonds[j]["height"] And spiders[i]["y"] + spiders[i]["height"] > diamonds[j]["y"] Then 
        If diamonds[j]["collected"] = "false" Then
          Shapes.HideShape(diamonds[j]["diamond"])
          diamonds[j]["collected"] = "true"
          ' Promote the spider
          If spiders[i]["lvl"] < 3 Then
            spiders[i]["lvl"] = spiders[i]["lvl"] + 1
            spiderNewImg = directory + "/objects/enemy-spider-" + spiders[i]["lvl"] + ".png"
            
            spiders[i]["hp"] = spiders[i]["hp"] + 1
            spiders[i]["speedX"] = spiders[i]["speedX"]*1.3
            spiders[i]["speedY"] = spiders[i]["speedY"]*1.3 
            Shapes.HideShape(spiders[i]["spider"])
            spiders[i]["spider"] = Shapes.AddImage(spiderNewImg)
          EndIf
        EndIf
      EndIf
    EndFor
  EndFor
EndSub

'=================== FIRE  ==============
Sub createFire
  fires[indexFire]["x"] = x
  fires[indexFire]["y"] = y
  fires[indexFire]["width"] = 40
  fires[indexFire]["height"] = 40
  
  fireImg = directory + "/objects/w11.png"
  fires[indexFire]["fire"] = Shapes.AddImage(fireImg)
  Shapes.Move(fires[indexFire]["fire"],fires[indexFire]["x"],fires[indexFire]["y"])
EndSub

Sub checkCollisionPlayerFire
  For i=1 To Array.GetItemCount(fires)
    If playerX < fires[i]["x"]+fires[i]["width"] And playerX + playerW > fires[i]["x"] And playerY < fires[i]["y"]+ fires[i]["height"] And playerY + playerH > fires[i]["y"] Then  
      score = score - 1
      updateScoreText()
    EndIf
  EndFor
EndSub

'=================== DIAMOND  ==============
Sub createDiamond
  diamonds[indexDiamond]["x"] = x
  diamonds[indexDiamond]["y"] = y
  diamonds[indexDiamond]["collected"] = "false"
  diamonds[indexDiamond]["width"] = 40
  diamonds[indexDiamond]["height"] = 40
  
  diamondImg = directory + "/objects/w10.png"
  diamonds[indexDiamond]["diamond"] = Shapes.AddImage(diamondImg)
  Shapes.Move(diamonds[indexDiamond]["diamond"],diamonds[indexDiamond]["x"],diamonds[indexDiamond]["y"])
EndSub

Sub checkCollisionPlayerDiamond
  For i=1 To Array.GetItemCount(diamonds)
    If playerX < diamonds[i]["x"]+diamonds[i]["width"] And playerX + playerW > diamonds[i]["x"] And playerY < diamonds[i]["y"]+ diamonds[i]["height"] And playerY + playerH > diamonds[i]["y"] Then 
      If diamonds[i]["collected"] = "false" Then
        Shapes.HideShape(diamonds[i]["diamond"])
        diamonds[i]["collected"] = "true"
        money = money + moneyPerDiamond
        updateMoneyText()
      EndIf
    EndIf
  EndFor
EndSub


'=================== START THE PROGRAM ==============
'=================== START THE PROGRAM ==============
'=================== START THE PROGRAM ==============
setup()
While notEnd="true"
  Program.Delay(1000/fps)
  loop()
EndWhile